amends "package://github.com/jdx/hk/releases/download/v1.26.0/hk@1.26.0#/Config.pkl"
import "package://github.com/jdx/hk/releases/download/v1.26.0/hk@1.26.0#/Builtins.pkl"

local linters = new Mapping<String, Step> {
    // Go linters
    ["go_fmt"] = Builtins.go_fmt
    ["go_imports"] = Builtins.go_imports
    ["go_vet"] {
        glob = "**/*.go"
        exclude = List("**/*_test.go")  // Skip test files (go test will vet them)
        check = "go vet ./..."
    }
    ["golangci_lint"] {
        glob = "**/*.go"
        exclude = List("**/*_test.go")  // Tests checked by go test
        check = "golangci-lint run --fix=false {{files}}"
        fix = "golangci-lint run --fix {{files}}"
    }
    ["gomod_tidy"] = Builtins.gomod_tidy

    // Markdown/config linters (only for generated docs, not source)
    ["prettier"] {
        glob = List("docs/**/*.md", "*.json", "*.yaml", "*.yml")
        exclude = List("pkgs/docs/**")  // Don't format source docs
        fix = "prettier --write {{files}}"
        check = "prettier --check {{files}}"
    }
    ["pkl"] {
        glob = "*.pkl"
        check = "pkl eval {{files}} >/dev/null"
    }
}

local testSteps = new Mapping<String, Step> {
    ["go_test"] {
        glob = "**/*.go"
        exclude = List("**/*_test.go", "dist/**", "vendor/**")
        check = "go test ./..."
    }
    ["md_process_blocks_test"] {
        glob = "pkgs/md-process-blocks-cli/**/*.go"
        check = "cd pkgs/md-process-blocks-cli && go test -v"
    }
}

local docSteps = new Mapping<String, Step> {
    ["docs_build"] {
        glob = "pkgs/docs/*.md"
        fix = "mise run docs-build"
        // Auto-stage generated docs after building
        stage = List("docs/**/*.md")
    }
}

hooks {
    ["pre-commit"] {
        fix = true    // automatically modify files with available linter fixes
        stash = "git" // stashes unstaged changes while running fix steps
        steps {
            ...docSteps  // build docs if source changed
            ...linters   // run all linters
            ...testSteps // run tests
        }
    }
    // instead of pre-commit, you can instead define pre-push hooks
    ["pre-push"] {
        steps {
            ...linters
            ...testSteps
        }
    }
    // "fix" and "check" are special steps for `hk fix` and `hk check` commands
    ["fix"] {
        fix = true
        steps = linters
    }
    ["check"] {
        steps {
            ...linters
            ...testSteps
        }
    }
}
